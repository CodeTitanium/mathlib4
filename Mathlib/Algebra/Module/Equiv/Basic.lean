/-
Copyright (c) 2020 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nathaniel Thomas, Jeremy Avigad, Johannes H√∂lzl, Mario Carneiro, Anne Baanen,
  Fr√©d√©ric Dupuis, Heather Macbeth
-/
import Mathlib.Algebra.Field.Defs
import Mathlib.Algebra.GroupWithZero.Action.Basic
import Mathlib.Algebra.GroupWithZero.Action.Units
import Mathlib.Algebra.Module.Equiv.Defs
import Mathlib.Algebra.Module.Hom
import Mathlib.Algebra.Module.LinearMap.End
import Mathlib.Algebra.Module.Pi
import Mathlib.Algebra.Module.Prod

/-!
# Further results on (semi)linear equivalences.
-/

open Function

variable {R : Type*} {R‚ÇÇ : Type*}
variable {K : Type*} {S : Type*} {M : Type*} {M‚ÇÅ : Type*} {M‚ÇÇ : Type*} {M‚ÇÉ : Type*}

section AddCommMonoid

namespace LinearEquiv

variable [Semiring R] [Semiring S] [Semiring R‚ÇÇ] [AddCommMonoid M] [AddCommMonoid M‚ÇÇ]

section RestrictScalars

variable (R)
variable [Module R M] [Module R M‚ÇÇ] [Module S M] [Module S M‚ÇÇ]
  [LinearMap.CompatibleSMul M M‚ÇÇ R S]

/-- If `M` and `M‚ÇÇ` are both `R`-semimodules and `S`-semimodules and `R`-semimodule structures
are defined by an action of `R` on `S` (formally, we have two scalar towers), then any `S`-linear
equivalence from `M` to `M‚ÇÇ` is also an `R`-linear equivalence.

See also `LinearMap.restrictScalars`. -/
@[simps]
def restrictScalars (f : M ‚âÉ‚Çó[S] M‚ÇÇ) : M ‚âÉ‚Çó[R] M‚ÇÇ :=
  { f.toLinearMap.restrictScalars R with
    toFun := f
    invFun := f.symm
    left_inv := f.left_inv
    right_inv := f.right_inv }

theorem restrictScalars_injective :
    Function.Injective (restrictScalars R : (M ‚âÉ‚Çó[S] M‚ÇÇ) ‚Üí M ‚âÉ‚Çó[R] M‚ÇÇ) := fun _ _ h ‚Ü¶
  ext (LinearEquiv.congr_fun h :)

@[simp]
theorem restrictScalars_inj (f g : M ‚âÉ‚Çó[S] M‚ÇÇ) :
    f.restrictScalars R = g.restrictScalars R ‚Üî f = g :=
  (restrictScalars_injective R).eq_iff

end RestrictScalars

theorem _root_.Module.End_isUnit_iff [Module R M] (f : Module.End R M) :
    IsUnit f ‚Üî Function.Bijective f :=
  ‚ü®fun h ‚Ü¶
    Function.bijective_iff_has_inverse.mpr <|
      ‚ü®h.unit.inv,
        ‚ü®Module.End_isUnit_inv_apply_apply_of_isUnit h,
        Module.End_isUnit_apply_inv_apply_of_isUnit h‚ü©‚ü©,
    fun H ‚Ü¶
    let e : M ‚âÉ‚Çó[R] M := { f, Equiv.ofBijective f H with }
    ‚ü®‚ü®_, e.symm, LinearMap.ext e.right_inv, LinearMap.ext e.left_inv‚ü©, rfl‚ü©‚ü©

section Automorphisms

variable [Module R M]

instance automorphismGroup : Group (M ‚âÉ‚Çó[R] M) where
  mul f g := g.trans f
  one := LinearEquiv.refl R M
  inv f := f.symm
  mul_assoc _ _ _ := rfl
  mul_one _ := ext fun _ ‚Ü¶ rfl
  one_mul _ := ext fun _ ‚Ü¶ rfl
  inv_mul_cancel f := ext <| f.left_inv

@[simp]
lemma coe_one : ‚Üë(1 : M ‚âÉ‚Çó[R] M) = id := rfl

@[simp]
lemma coe_toLinearMap_one : (‚Üë(1 : M ‚âÉ‚Çó[R] M) : M ‚Üí‚Çó[R] M) = LinearMap.id := rfl

@[simp]
lemma coe_toLinearMap_mul {e‚ÇÅ e‚ÇÇ : M ‚âÉ‚Çó[R] M} :
    (‚Üë(e‚ÇÅ * e‚ÇÇ) : M ‚Üí‚Çó[R] M) = (e‚ÇÅ : M ‚Üí‚Çó[R] M) * (e‚ÇÇ : M ‚Üí‚Çó[R] M) :=
  rfl

theorem coe_pow (e : M ‚âÉ‚Çó[R] M) (n : ‚Ñï) : ‚áë(e ^ n) = e^[n] := hom_coe_pow _ rfl (fun _ _ ‚Ü¶ rfl) _ _

theorem pow_apply (e : M ‚âÉ‚Çó[R] M) (n : ‚Ñï) (m : M) : (e ^ n) m = e^[n] m := congr_fun (coe_pow e n) m

@[simp] lemma mul_apply (f : M ‚âÉ‚Çó[R] M) (g : M ‚âÉ‚Çó[R] M) (x : M) : (f * g) x = f (g x) := rfl

/-- Restriction from `R`-linear automorphisms of `M` to `R`-linear endomorphisms of `M`,
promoted to a monoid hom. -/
@[simps]
def automorphismGroup.toLinearMapMonoidHom : (M ‚âÉ‚Çó[R] M) ‚Üí* M ‚Üí‚Çó[R] M where
  toFun e := e.toLinearMap
  map_one' := rfl
  map_mul' _ _ := rfl

/-- The tautological action by `M ‚âÉ‚Çó[R] M` on `M`.

This generalizes `Function.End.applyMulAction`. -/
instance applyDistribMulAction : DistribMulAction (M ‚âÉ‚Çó[R] M) M where
  smul := (¬∑ <| ¬∑)
  smul_zero := LinearEquiv.map_zero
  smul_add := LinearEquiv.map_add
  one_smul _ := rfl
  mul_smul _ _ _ := rfl

@[simp]
protected theorem smul_def (f : M ‚âÉ‚Çó[R] M) (a : M) : f ‚Ä¢ a = f a :=
  rfl

/-- `LinearEquiv.applyDistribMulAction` is faithful. -/
instance apply_faithfulSMul : FaithfulSMul (M ‚âÉ‚Çó[R] M) M :=
  ‚ü®LinearEquiv.ext‚ü©

instance apply_smulCommClass [SMul S R] [SMul S M] [IsScalarTower S R M] :
    SMulCommClass S (M ‚âÉ‚Çó[R] M) M where
  smul_comm r e m := (e.map_smul_of_tower r m).symm

instance apply_smulCommClass' [SMul S R] [SMul S M] [IsScalarTower S R M] :
    SMulCommClass (M ‚âÉ‚Çó[R] M) S M :=
  SMulCommClass.symm _ _ _

end Automorphisms

section OfSubsingleton

variable (M M‚ÇÇ)
variable [Module R M] [Module R M‚ÇÇ] [Subsingleton M] [Subsingleton M‚ÇÇ]

/-- Any two modules that are subsingletons are isomorphic. -/
@[simps]
def ofSubsingleton : M ‚âÉ‚Çó[R] M‚ÇÇ :=
  { (0 : M ‚Üí‚Çó[R] M‚ÇÇ) with
    toFun := fun _ ‚Ü¶ 0
    invFun := fun _ ‚Ü¶ 0
    left_inv := fun _ ‚Ü¶ Subsingleton.elim _ _
    right_inv := fun _ ‚Ü¶ Subsingleton.elim _ _ }

@[simp]
theorem ofSubsingleton_self : ofSubsingleton M M = refl R M := by
  ext
  simp [eq_iff_true_of_subsingleton]

end OfSubsingleton

end LinearEquiv

namespace Module

/-- `g : R ‚âÉ+* S` is `R`-linear when the module structure on `S` is `Module.compHom S g` . -/
@[simps]
def compHom.toLinearEquiv {R S : Type*} [Semiring R] [Semiring S] (g : R ‚âÉ+* S) :
    haveI := compHom S (‚Üëg : R ‚Üí+* S)
    R ‚âÉ‚Çó[R] S :=
  letI := compHom S (‚Üëg : R ‚Üí+* S)
  { g with
    toFun := (g : R ‚Üí S)
    invFun := (g.symm : S ‚Üí R)
    map_smul' := g.map_mul }

end Module

namespace DistribMulAction

variable (R M) [Semiring R] [AddCommMonoid M] [Module R M]
variable [Group S] [DistribMulAction S M] [SMulCommClass S R M]

/-- Each element of the group defines a linear equivalence.

This is a stronger version of `DistribMulAction.toAddEquiv`. -/
@[simps!]
def toLinearEquiv (s : S) : M ‚âÉ‚Çó[R] M :=
  { toAddEquiv M s, toLinearMap R M s with }

/-- Each element of the group defines a module automorphism.

This is a stronger version of `DistribMulAction.toAddAut`. -/
@[simps]
def toModuleAut : S ‚Üí* M ‚âÉ‚Çó[R] M where
  toFun := toLinearEquiv R M
  map_one' := LinearEquiv.ext <| one_smul _
  map_mul' _ _ := LinearEquiv.ext <| mul_smul _ _

end DistribMulAction

namespace AddEquiv

section AddCommMonoid

variable [Semiring R] [AddCommMonoid M] [AddCommMonoid M‚ÇÇ] [AddCommMonoid M‚ÇÉ]
variable [Module R M] [Module R M‚ÇÇ]
variable (e : M ‚âÉ+ M‚ÇÇ)

/-- An additive equivalence whose underlying function preserves `smul` is a linear equivalence. -/
def toLinearEquiv (h : ‚àÄ (c : R) (x), e (c ‚Ä¢ x) = c ‚Ä¢ e x) : M ‚âÉ‚Çó[R] M‚ÇÇ :=
  { e with map_smul' := h }

@[simp]
theorem coe_toLinearEquiv (h : ‚àÄ (c : R) (x), e (c ‚Ä¢ x) = c ‚Ä¢ e x) : ‚áë(e.toLinearEquiv h) = e :=
  rfl

@[simp]
theorem coe_toLinearEquiv_symm (h : ‚àÄ (c : R) (x), e (c ‚Ä¢ x) = c ‚Ä¢ e x) :
    ‚áë(e.toLinearEquiv h).symm = e.symm :=
  rfl

/-- An additive equivalence between commutative additive monoids is a linear equivalence between
‚Ñï-modules -/
def toNatLinearEquiv : M ‚âÉ‚Çó[‚Ñï] M‚ÇÇ :=
  e.toLinearEquiv fun c a ‚Ü¶ by rw [map_nsmul]

@[simp]
theorem coe_toNatLinearEquiv : ‚áëe.toNatLinearEquiv = e :=
  rfl

@[simp]
theorem toNatLinearEquiv_toAddEquiv : ‚Üëe.toNatLinearEquiv = e :=
  rfl

@[simp]
theorem _root_.LinearEquiv.toAddEquiv_toNatLinearEquiv (e : M ‚âÉ‚Çó[‚Ñï] M‚ÇÇ) :
    AddEquiv.toNatLinearEquiv ‚Üëe = e :=
  DFunLike.coe_injective rfl

@[simp]
theorem toNatLinearEquiv_symm : e.toNatLinearEquiv.symm = e.symm.toNatLinearEquiv :=
  rfl

@[simp]
theorem toNatLinearEquiv_refl : (AddEquiv.refl M).toNatLinearEquiv = LinearEquiv.refl ‚Ñï M :=
  rfl

@[simp]
theorem toNatLinearEquiv_trans (e‚ÇÇ : M‚ÇÇ ‚âÉ+ M‚ÇÉ) :
    e.toNatLinearEquiv.trans e‚ÇÇ.toNatLinearEquiv = (e.trans e‚ÇÇ).toNatLinearEquiv :=
  rfl

end AddCommMonoid

section AddCommGroup

variable [AddCommGroup M] [AddCommGroup M‚ÇÇ] [AddCommGroup M‚ÇÉ]
variable (e : M ‚âÉ+ M‚ÇÇ)

/-- An additive equivalence between commutative additive groups is a linear
equivalence between ‚Ñ§-modules -/
def toIntLinearEquiv : M ‚âÉ‚Çó[‚Ñ§] M‚ÇÇ :=
  e.toLinearEquiv fun c a ‚Ü¶ e.toAddMonoidHom.map_zsmul a c

@[simp]
theorem coe_toIntLinearEquiv : ‚áëe.toIntLinearEquiv = e :=
  rfl

@[simp]
theorem toIntLinearEquiv_toAddEquiv : ‚Üëe.toIntLinearEquiv = e := by
  ext
  rfl

@[simp]
theorem _root_.LinearEquiv.toAddEquiv_toIntLinearEquiv (e : M ‚âÉ‚Çó[‚Ñ§] M‚ÇÇ) :
    AddEquiv.toIntLinearEquiv (e : M ‚âÉ+ M‚ÇÇ) = e :=
  DFunLike.coe_injective rfl

@[simp]
theorem toIntLinearEquiv_symm : e.toIntLinearEquiv.symm = e.symm.toIntLinearEquiv :=
  rfl

@[simp]
theorem toIntLinearEquiv_refl : (AddEquiv.refl M).toIntLinearEquiv = LinearEquiv.refl ‚Ñ§ M :=
  rfl

@[simp]
theorem toIntLinearEquiv_trans (e‚ÇÇ : M‚ÇÇ ‚âÉ+ M‚ÇÉ) :
    e.toIntLinearEquiv.trans e‚ÇÇ.toIntLinearEquiv = (e.trans e‚ÇÇ).toIntLinearEquiv :=
  rfl

end AddCommGroup

end AddEquiv

namespace LinearMap

variable (R S M)
variable [Semiring R] [Semiring S] [AddCommMonoid M] [Module R M]

/-- The equivalence between R-linear maps from `R` to `M`, and points of `M` itself.
This says that the forgetful functor from `R`-modules to types is representable, by `R`.

This is an `S`-linear equivalence, under the assumption that `S` acts on `M` commuting with `R`.
When `R` is commutative, we can take this to be the usual action with `S = R`.
Otherwise, `S = ‚Ñï` shows that the equivalence is additive.
See note [bundled maps over different rings].
-/
@[simps]
def ringLmapEquivSelf [Module S M] [SMulCommClass R S M] : (R ‚Üí‚Çó[R] M) ‚âÉ‚Çó[S] M :=
  { apply‚Çó' S (1 : R) with
    toFun := fun f ‚Ü¶ f 1
    invFun := smulRight (1 : R ‚Üí‚Çó[R] R)
    left_inv := fun f ‚Ü¶ by
      ext
      simp only [coe_smulRight, one_apply, smul_eq_mul, ‚Üê map_smul f, mul_one]
    right_inv := fun x ‚Ü¶ by simp }

end LinearMap

/--
The `R`-linear equivalence between additive morphisms `A ‚Üí+ B` and `‚Ñï`-linear morphisms `A ‚Üí‚Çó[‚Ñï] B`.
-/
@[simps]
def addMonoidHomLequivNat {A B : Type*} (R : Type*) [Semiring R] [AddCommMonoid A]
    [AddCommMonoid B] [Module R B] : (A ‚Üí+ B) ‚âÉ‚Çó[R] A ‚Üí‚Çó[‚Ñï] B where
  toFun := AddMonoidHom.toNatLinearMap
  invFun := LinearMap.toAddMonoidHom
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  left_inv _ := rfl
  right_inv _ := rfl

/--
The `R`-linear equivalence between additive morphisms `A ‚Üí+ B` and `‚Ñ§`-linear morphisms `A ‚Üí‚Çó[‚Ñ§] B`.
-/
@[simps]
def addMonoidHomLequivInt {A B : Type*} (R : Type*) [Semiring R] [AddCommGroup A] [AddCommGroup B]
    [Module R B] : (A ‚Üí+ B) ‚âÉ‚Çó[R] A ‚Üí‚Çó[‚Ñ§] B where
  toFun := AddMonoidHom.toIntLinearMap
  invFun := LinearMap.toAddMonoidHom
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  left_inv _ := rfl
  right_inv _ := rfl

/-- Ring equivalence between additive group endomorphisms of an `AddCommGroup` `A` and
`‚Ñ§`-module endomorphisms of `A.` -/
@[simps] def addMonoidEndRingEquivInt (A : Type*) [AddCommGroup A] :
    AddMonoid.End A ‚âÉ+* Module.End ‚Ñ§ A :=
  { addMonoidHomLequivInt (B := A) ‚Ñ§ with
    map_mul' := fun _ _ ‚Ü¶ rfl }

namespace LinearEquiv

section AddCommMonoid

section Subsingleton

variable [Semiring R] [Semiring R‚ÇÇ]
variable [AddCommMonoid M] [AddCommMonoid M‚ÇÇ]
variable [Module R M] [Module R‚ÇÇ M‚ÇÇ]
variable {œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ} {œÉ‚ÇÇ‚ÇÅ : R‚ÇÇ ‚Üí+* R}
variable [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ] [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ]

section Module

variable [Subsingleton M] [Subsingleton M‚ÇÇ]

/-- Between two zero modules, the zero map is an equivalence. -/
instance : Zero (M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) :=
  ‚ü®{ (0 : M ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) with
      toFun := 0
      invFun := 0
      right_inv := Subsingleton.elim _
      left_inv := Subsingleton.elim _ }‚ü©

-- Even though these are implied by `Subsingleton.elim` via the `Unique` instance below, they're
-- nice to have as `rfl`-lemmas for `dsimp`.
@[simp]
theorem zero_symm : (0 : M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ).symm = 0 :=
  rfl

@[simp]
theorem coe_zero : ‚áë(0 : M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) = 0 :=
  rfl

theorem zero_apply (x : M) : (0 : M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) x = 0 :=
  rfl

/-- Between two zero modules, the zero map is the only equivalence. -/
instance : Unique (M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) where
  uniq _ := toLinearMap_injective (Subsingleton.elim _ _)
  default := 0

end Module

instance uniqueOfSubsingleton [Subsingleton R] [Subsingleton R‚ÇÇ] : Unique (M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) := by
  haveI := Module.subsingleton R M
  haveI := Module.subsingleton R‚ÇÇ M‚ÇÇ
  infer_instance

end Subsingleton

section Uncurry

variable [Semiring R]
variable [AddCommMonoid M] [Module R M]
variable (V V‚ÇÇ R M)

/-- Linear equivalence between a curried and uncurried function.
  Differs from `TensorProduct.curry`. -/
protected def curry : (V √ó V‚ÇÇ ‚Üí M) ‚âÉ‚Çó[R] V ‚Üí V‚ÇÇ ‚Üí M :=
  { Equiv.curry _ _ _ with
    map_add' := fun _ _ ‚Ü¶ rfl
    map_smul' := fun _ _ ‚Ü¶ rfl }

@[simp]
theorem coe_curry : ‚áë(LinearEquiv.curry R M V V‚ÇÇ) = curry :=
  rfl

@[simp]
theorem coe_curry_symm : ‚áë(LinearEquiv.curry R M V V‚ÇÇ).symm = uncurry :=
  rfl

end Uncurry

section

variable [Semiring R] [Semiring R‚ÇÇ]
variable [AddCommMonoid M] [AddCommMonoid M‚ÇÇ]
variable {module_M : Module R M} {module_M‚ÇÇ : Module R‚ÇÇ M‚ÇÇ}
variable {œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ} {œÉ‚ÇÇ‚ÇÅ : R‚ÇÇ ‚Üí+* R}
variable {re‚ÇÅ‚ÇÇ : RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ} {re‚ÇÇ‚ÇÅ : RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ}
variable (f : M ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (g : M‚ÇÇ ‚Üí‚Çõ‚Çó[œÉ‚ÇÇ‚ÇÅ] M)


/-- If a linear map has an inverse, it is a linear equivalence. -/
def ofLinear (h‚ÇÅ : f.comp g = LinearMap.id) (h‚ÇÇ : g.comp f = LinearMap.id) : M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ :=
  { f with
    invFun := g
    left_inv := LinearMap.ext_iff.1 h‚ÇÇ
    right_inv := LinearMap.ext_iff.1 h‚ÇÅ }

@[simp]
theorem ofLinear_apply {h‚ÇÅ h‚ÇÇ} (x : M) : (ofLinear f g h‚ÇÅ h‚ÇÇ : M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) x = f x :=
  rfl

@[simp]
theorem ofLinear_symm_apply {h‚ÇÅ h‚ÇÇ} (x : M‚ÇÇ) : (ofLinear f g h‚ÇÅ h‚ÇÇ : M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ).symm x = g x :=
  rfl

@[simp]
theorem ofLinear_toLinearMap {h‚ÇÅ h‚ÇÇ} : (ofLinear f g h‚ÇÅ h‚ÇÇ : M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) = f := rfl

@[simp]
theorem ofLinear_symm_toLinearMap {h‚ÇÅ h‚ÇÇ} : (ofLinear f g h‚ÇÅ h‚ÇÇ : M ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ).symm = g := rfl

end

end AddCommMonoid

section Neg

variable (R) [Semiring R] [AddCommGroup M] [Module R M]

/-- `x ‚Ü¶ -x` as a `LinearEquiv` -/
def neg : M ‚âÉ‚Çó[R] M :=
  { Equiv.neg M, (-LinearMap.id : M ‚Üí‚Çó[R] M) with }

variable {R}

@[simp]
theorem coe_neg : ‚áë(neg R : M ‚âÉ‚Çó[R] M) = -id :=
  rfl

theorem neg_apply (x : M) : neg R x = -x := by simp

@[simp]
theorem symm_neg : (neg R : M ‚âÉ‚Çó[R] M).symm = neg R :=
  rfl

end Neg

section Semiring

section

open LinearMap

variable {M‚ÇÇ‚ÇÅ M‚ÇÇ‚ÇÇ : Type*} [Semiring R] [AddCommMonoid M‚ÇÅ] [AddCommMonoid M‚ÇÇ]
  [AddCommMonoid M‚ÇÇ‚ÇÅ] [AddCommMonoid M‚ÇÇ‚ÇÇ] [Module R M‚ÇÅ] [Module R M‚ÇÇ] [Module R M‚ÇÇ‚ÇÅ] [Module R M‚ÇÇ‚ÇÇ]

/-- A linear isomorphism between the domains and codomains of two spaces of linear maps gives a
additive isomorphism between the two function spaces.

See also `LinearEquiv.arrowCongr` for the linear version of this isomorphism. -/
def arrowCongrAddEquiv (e‚ÇÅ : M‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ‚ÇÇ) :
    (M‚ÇÅ ‚Üí‚Çó[R] M‚ÇÇ‚ÇÅ) ‚âÉ+ (M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÇ‚ÇÇ) where
  toFun f := e‚ÇÇ.comp (f.comp e‚ÇÅ.symm.toLinearMap)
  invFun f := e‚ÇÇ.symm.comp (f.comp e‚ÇÅ.toLinearMap)
  left_inv f := by
    ext x
    simp only [symm_apply_apply, Function.comp_apply, coe_comp, coe_coe]
  right_inv f := by
    ext x
    simp only [Function.comp_apply, apply_symm_apply, coe_comp, coe_coe]
  map_add' f g := by
    ext x
    simp only [map_add, add_apply, Function.comp_apply, coe_comp, coe_coe]

/-- If `M` and `M‚ÇÇ` are linearly isomorphic then the endomorphism rings of `M` and `M‚ÇÇ`
are isomorphic.

See `LinearEquiv.conj` for the linear version of this isomorphism. -/
def conjRingEquiv (e : M‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ) : Module.End R M‚ÇÅ ‚âÉ+* Module.End R M‚ÇÇ where
  __ := arrowCongrAddEquiv e e
  map_mul' _ _ := by ext; simp [arrowCongrAddEquiv]

end

section Module

variable {R : Type*} [Semiring R]
  {E G : Type*} [AddCommMonoid E] [Module R E] [AddCommMonoid G] [Module R G] [Unique G]

variable (R E) in
/-- The natural equivalence `E √ó G ‚âÉ‚Çó[ùïú] E` for any `Unique` type `G`.
This is `Equiv.prodUnique` as a linear equivalence. -/
def prodUnique : (E √ó G) ‚âÉ‚Çó[R] E where
  toEquiv := Equiv.prodUnique E G
  map_add' x y := by simp
  map_smul' r x := by simp

@[simp]
lemma prodUnique_toEquiv : (prodUnique R E).toEquiv = Equiv.prodUnique E G := rfl

end Module

end Semiring

section CommSemiring

variable [CommSemiring R] [AddCommMonoid M] [AddCommMonoid M‚ÇÇ] [AddCommMonoid M‚ÇÉ]
variable [Module R M] [Module R M‚ÇÇ] [Module R M‚ÇÉ]

open LinearMap

/-- Multiplying by a unit `a` of the ring `R` is a linear equivalence. -/
def smulOfUnit (a : RÀ£) : M ‚âÉ‚Çó[R] M :=
  DistribMulAction.toLinearEquiv R M a

/-- A linear isomorphism between the domains and codomains of two spaces of linear maps gives a
linear isomorphism between the two function spaces.

See `LinearEquiv.arrowCongrAddEquiv` for the additive version of this isomorphism that works
over a not necessarily commutative semiring. -/
def arrowCongr {R M‚ÇÅ M‚ÇÇ M‚ÇÇ‚ÇÅ M‚ÇÇ‚ÇÇ : Sort _} [CommSemiring R] [AddCommMonoid M‚ÇÅ] [AddCommMonoid M‚ÇÇ]
    [AddCommMonoid M‚ÇÇ‚ÇÅ] [AddCommMonoid M‚ÇÇ‚ÇÇ] [Module R M‚ÇÅ] [Module R M‚ÇÇ] [Module R M‚ÇÇ‚ÇÅ]
    [Module R M‚ÇÇ‚ÇÇ] (e‚ÇÅ : M‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ‚ÇÇ) : (M‚ÇÅ ‚Üí‚Çó[R] M‚ÇÇ‚ÇÅ) ‚âÉ‚Çó[R] M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÇ‚ÇÇ where
  __ := arrowCongrAddEquiv e‚ÇÅ e‚ÇÇ
  map_smul' c f := by ext; simp [arrowCongrAddEquiv]

@[simp]
theorem arrowCongr_apply {R M‚ÇÅ M‚ÇÇ M‚ÇÇ‚ÇÅ M‚ÇÇ‚ÇÇ : Sort _} [CommSemiring R] [AddCommMonoid M‚ÇÅ]
    [AddCommMonoid M‚ÇÇ] [AddCommMonoid M‚ÇÇ‚ÇÅ] [AddCommMonoid M‚ÇÇ‚ÇÇ] [Module R M‚ÇÅ] [Module R M‚ÇÇ]
    [Module R M‚ÇÇ‚ÇÅ] [Module R M‚ÇÇ‚ÇÇ] (e‚ÇÅ : M‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ‚ÇÇ) (f : M‚ÇÅ ‚Üí‚Çó[R] M‚ÇÇ‚ÇÅ)
    (x : M‚ÇÇ) : arrowCongr e‚ÇÅ e‚ÇÇ f x = e‚ÇÇ (f (e‚ÇÅ.symm x)) :=
  rfl

@[simp]
theorem arrowCongr_symm_apply {R M‚ÇÅ M‚ÇÇ M‚ÇÇ‚ÇÅ M‚ÇÇ‚ÇÇ : Sort _} [CommSemiring R] [AddCommMonoid M‚ÇÅ]
    [AddCommMonoid M‚ÇÇ] [AddCommMonoid M‚ÇÇ‚ÇÅ] [AddCommMonoid M‚ÇÇ‚ÇÇ] [Module R M‚ÇÅ] [Module R M‚ÇÇ]
    [Module R M‚ÇÇ‚ÇÅ] [Module R M‚ÇÇ‚ÇÇ] (e‚ÇÅ : M‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ‚ÇÇ) (f : M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÇ‚ÇÇ)
    (x : M‚ÇÅ) : (arrowCongr e‚ÇÅ e‚ÇÇ).symm f x = e‚ÇÇ.symm (f (e‚ÇÅ x)) :=
  rfl

theorem arrowCongr_comp {N N‚ÇÇ N‚ÇÉ : Sort _} [AddCommMonoid N] [AddCommMonoid N‚ÇÇ] [AddCommMonoid N‚ÇÉ]
    [Module R N] [Module R N‚ÇÇ] [Module R N‚ÇÉ] (e‚ÇÅ : M ‚âÉ‚Çó[R] N) (e‚ÇÇ : M‚ÇÇ ‚âÉ‚Çó[R] N‚ÇÇ) (e‚ÇÉ : M‚ÇÉ ‚âÉ‚Çó[R] N‚ÇÉ)
    (f : M ‚Üí‚Çó[R] M‚ÇÇ) (g : M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÉ) :
    arrowCongr e‚ÇÅ e‚ÇÉ (g.comp f) = (arrowCongr e‚ÇÇ e‚ÇÉ g).comp (arrowCongr e‚ÇÅ e‚ÇÇ f) := by
  ext
  simp only [symm_apply_apply, arrowCongr_apply, LinearMap.comp_apply]

theorem arrowCongr_trans {M‚ÇÅ M‚ÇÇ M‚ÇÉ N‚ÇÅ N‚ÇÇ N‚ÇÉ : Sort _} [AddCommMonoid M‚ÇÅ] [Module R M‚ÇÅ]
    [AddCommMonoid M‚ÇÇ] [Module R M‚ÇÇ] [AddCommMonoid M‚ÇÉ] [Module R M‚ÇÉ] [AddCommMonoid N‚ÇÅ]
    [Module R N‚ÇÅ] [AddCommMonoid N‚ÇÇ] [Module R N‚ÇÇ] [AddCommMonoid N‚ÇÉ] [Module R N‚ÇÉ]
    (e‚ÇÅ : M‚ÇÅ ‚âÉ‚Çó[R] M‚ÇÇ) (e‚ÇÇ : N‚ÇÅ ‚âÉ‚Çó[R] N‚ÇÇ) (e‚ÇÉ : M‚ÇÇ ‚âÉ‚Çó[R] M‚ÇÉ) (e‚ÇÑ : N‚ÇÇ ‚âÉ‚Çó[R] N‚ÇÉ) :
    (arrowCongr e‚ÇÅ e‚ÇÇ).trans (arrowCongr e‚ÇÉ e‚ÇÑ) = arrowCongr (e‚ÇÅ.trans e‚ÇÉ) (e‚ÇÇ.trans e‚ÇÑ) :=
  rfl

/-- If `M‚ÇÇ` and `M‚ÇÉ` are linearly isomorphic then the two spaces of linear maps from `M` into `M‚ÇÇ`
and `M` into `M‚ÇÉ` are linearly isomorphic. -/
def congrRight (f : M‚ÇÇ ‚âÉ‚Çó[R] M‚ÇÉ) : (M ‚Üí‚Çó[R] M‚ÇÇ) ‚âÉ‚Çó[R] M ‚Üí‚Çó[R] M‚ÇÉ :=
  arrowCongr (LinearEquiv.refl R M) f

/-- If `M` and `M‚ÇÇ` are linearly isomorphic then the two spaces of linear maps from `M` and `M‚ÇÇ` to
themselves are linearly isomorphic.

See `LinearEquiv.conjRingEquiv` for the isomorphism between endomorphism rings,
which works over a not necessarily commutative semiring. -/
-- TODO: upgrade to AlgEquiv (but this file currently cannot import AlgEquiv)
def conj (e : M ‚âÉ‚Çó[R] M‚ÇÇ) : Module.End R M ‚âÉ‚Çó[R] Module.End R M‚ÇÇ :=
  arrowCongr e e

theorem conj_apply (e : M ‚âÉ‚Çó[R] M‚ÇÇ) (f : Module.End R M) :
    e.conj f = ((‚Üëe : M ‚Üí‚Çó[R] M‚ÇÇ).comp f).comp (e.symm : M‚ÇÇ ‚Üí‚Çó[R] M) :=
  rfl

theorem conj_apply_apply (e : M ‚âÉ‚Çó[R] M‚ÇÇ) (f : Module.End R M) (x : M‚ÇÇ) :
    e.conj f x = e (f (e.symm x)) :=
  rfl

theorem symm_conj_apply (e : M ‚âÉ‚Çó[R] M‚ÇÇ) (f : Module.End R M‚ÇÇ) :
    e.symm.conj f = ((‚Üëe.symm : M‚ÇÇ ‚Üí‚Çó[R] M).comp f).comp (e : M ‚Üí‚Çó[R] M‚ÇÇ) :=
  rfl

theorem conj_comp (e : M ‚âÉ‚Çó[R] M‚ÇÇ) (f g : Module.End R M) :
    e.conj (g.comp f) = (e.conj g).comp (e.conj f) :=
  arrowCongr_comp e e e f g

theorem conj_trans (e‚ÇÅ : M ‚âÉ‚Çó[R] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ ‚âÉ‚Çó[R] M‚ÇÉ) :
    e‚ÇÅ.conj.trans e‚ÇÇ.conj = (e‚ÇÅ.trans e‚ÇÇ).conj :=
  rfl

@[simp]
theorem conj_id (e : M ‚âÉ‚Çó[R] M‚ÇÇ) : e.conj LinearMap.id = LinearMap.id := by
  ext
  simp [conj_apply]

@[simp]
theorem conj_refl (f : Module.End R M) : (refl R M).conj f = f := rfl

variable (M) in
/-- An `R`-linear isomorphism between two `R`-modules `M‚ÇÇ` and `M‚ÇÉ` induces an `S`-linear
isomorphism between `M‚ÇÇ ‚Üí‚Çó[R] M` and `M‚ÇÉ ‚Üí‚Çó[R] M`, if `M` is both an `R`-module and an
`S`-module and their actions commute. -/
def congrLeft {R} (S) [Semiring R] [Semiring S] [Module R M‚ÇÇ] [Module R M‚ÇÉ] [Module R M]
    [Module S M] [SMulCommClass R S M] (e : M‚ÇÇ ‚âÉ‚Çó[R] M‚ÇÉ) : (M‚ÇÇ ‚Üí‚Çó[R] M) ‚âÉ‚Çó[S] (M‚ÇÉ ‚Üí‚Çó[R] M) where
  __ := e.arrowCongrAddEquiv (.refl ..)
  map_smul' _ _ := rfl

end CommSemiring

section Field

variable [Field K] [AddCommGroup M] [Module K M]
variable (K) (M)

open LinearMap

/-- Multiplying by a nonzero element `a` of the field `K` is a linear equivalence. -/
@[simps!]
def smulOfNeZero (a : K) (ha : a ‚â† 0) : M ‚âÉ‚Çó[K] M :=
  smulOfUnit <| Units.mk0 a ha

end Field

end LinearEquiv

namespace Equiv

variable [Semiring R] [AddCommMonoid M] [Module R M] [AddCommMonoid M‚ÇÇ] [Module R M‚ÇÇ]

/-- An equivalence whose underlying function is linear is a linear equivalence. -/
def toLinearEquiv (e : M ‚âÉ M‚ÇÇ) (h : IsLinearMap R (e : M ‚Üí M‚ÇÇ)) : M ‚âÉ‚Çó[R] M‚ÇÇ :=
  { e, h.mk' e with }

end Equiv

section FunLeft

variable (R M) [Semiring R] [AddCommMonoid M] [Module R M]
variable {m n p : Type*}

namespace LinearMap

/-- Given an `R`-module `M` and a function `m ‚Üí n` between arbitrary types,
construct a linear map `(n ‚Üí M) ‚Üí‚Çó[R] (m ‚Üí M)` -/
def funLeft (f : m ‚Üí n) : (n ‚Üí M) ‚Üí‚Çó[R] m ‚Üí M where
  toFun := (¬∑ ‚àò f)
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

@[simp]
theorem funLeft_apply (f : m ‚Üí n) (g : n ‚Üí M) (i : m) : funLeft R M f g i = g (f i) :=
  rfl

@[simp]
theorem funLeft_id (g : n ‚Üí M) : funLeft R M _root_.id g = g :=
  rfl

theorem funLeft_comp (f‚ÇÅ : n ‚Üí p) (f‚ÇÇ : m ‚Üí n) :
    funLeft R M (f‚ÇÅ ‚àò f‚ÇÇ) = (funLeft R M f‚ÇÇ).comp (funLeft R M f‚ÇÅ) :=
  rfl

theorem funLeft_surjective_of_injective (f : m ‚Üí n) (hf : Injective f) :
    Surjective (funLeft R M f) :=
  hf.surjective_comp_right

theorem funLeft_injective_of_surjective (f : m ‚Üí n) (hf : Surjective f) :
    Injective (funLeft R M f) :=
  hf.injective_comp_right

end LinearMap

namespace LinearEquiv

open LinearMap

/-- Given an `R`-module `M` and an equivalence `m ‚âÉ n` between arbitrary types,
construct a linear equivalence `(n ‚Üí M) ‚âÉ‚Çó[R] (m ‚Üí M)` -/
def funCongrLeft (e : m ‚âÉ n) : (n ‚Üí M) ‚âÉ‚Çó[R] m ‚Üí M :=
  LinearEquiv.ofLinear (funLeft R M e) (funLeft R M e.symm)
    (LinearMap.ext fun x ‚Ü¶
      funext fun i ‚Ü¶ by rw [id_apply, ‚Üê funLeft_comp, Equiv.symm_comp_self, LinearMap.funLeft_id])
    (LinearMap.ext fun x ‚Ü¶
      funext fun i ‚Ü¶ by rw [id_apply, ‚Üê funLeft_comp, Equiv.self_comp_symm, LinearMap.funLeft_id])

@[simp]
theorem funCongrLeft_apply (e : m ‚âÉ n) (x : n ‚Üí M) : funCongrLeft R M e x = funLeft R M e x :=
  rfl

@[simp]
theorem funCongrLeft_id : funCongrLeft R M (Equiv.refl n) = LinearEquiv.refl R (n ‚Üí M) :=
  rfl

@[simp]
theorem funCongrLeft_comp (e‚ÇÅ : m ‚âÉ n) (e‚ÇÇ : n ‚âÉ p) :
    funCongrLeft R M (Equiv.trans e‚ÇÅ e‚ÇÇ) =
      LinearEquiv.trans (funCongrLeft R M e‚ÇÇ) (funCongrLeft R M e‚ÇÅ) :=
  rfl

@[simp]
theorem funCongrLeft_symm (e : m ‚âÉ n) : (funCongrLeft R M e).symm = funCongrLeft R M e.symm :=
  rfl

end LinearEquiv

end FunLeft

section Pi

namespace LinearEquiv

/-- The product over `S ‚äï T` of a family of modules is isomorphic to the product of
(the product over `S`) and (the product over `T`).

This is `Equiv.sumPiEquivProdPi` as a `LinearEquiv`.
-/
@[simps -fullyApplied +simpRhs]
def sumPiEquivProdPi (R : Type*) [Semiring R] (S T : Type*) (A : S ‚äï T ‚Üí Type*)
    [‚àÄ st, AddCommMonoid (A st)] [‚àÄ st, Module R (A st)] :
    (Œ† (st : S ‚äï T), A st) ‚âÉ‚Çó[R] (Œ† (s : S), A (.inl s)) √ó (Œ† (t : T), A (.inr t)) where
  __ := Equiv.sumPiEquivProdPi _
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

/-- The product `Œ† t : Œ±, f t` of a family of modules is linearly isomorphic to the module
`f ‚¨ù` when `Œ±` only contains `‚¨ù`.

This is `Equiv.piUnique` as a `LinearEquiv`.
-/
@[simps -fullyApplied]
def piUnique {Œ± : Type*} [Unique Œ±] (R : Type*) [Semiring R] (f : Œ± ‚Üí Type*)
    [‚àÄ x, AddCommMonoid (f x)] [‚àÄ x, Module R (f x)] : (Œ† t : Œ±, f t) ‚âÉ‚Çó[R] f default where
  __ := Equiv.piUnique _
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

end LinearEquiv

end Pi

end AddCommMonoid
