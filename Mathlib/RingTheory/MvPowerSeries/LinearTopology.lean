/-
Copyright (c) 2024 Antoine Chambert-Loir, Mar√≠a In√©s de Frutos-Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir, Mar√≠a In√©s de Frutos-Fern√°ndez
-/
import Mathlib.Data.Finsupp.Interval
import Mathlib.RingTheory.MvPowerSeries.PiTopology
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.Nonarchimedean.Bases

/-! # Linear topology on the ring of multivariate power series

- `MvPowerSeries.basis`: the ideals of the ring of multivariate power series
all coefficients the exponent of which is smaller than some bound vanish.

## Instances :

- `MvPowerSeries.linearTopology`.

TODO. For the moment, this is restricted to commutative rings because of the similar
restriction for linear topologies. However, the definition below is already correct
in the general case, the issue is solely about the definition of a linear topology.

-/
namespace MvPowerSeries

namespace LinearTopology

open scoped Topology

open Set SetLike

/-- The underlying family for the basis of ideals in a multivariate power series ring. -/
def basis (œÉ : Type*) (Œ± : Type*) [Ring Œ±] :
    (œÉ ‚Üí‚ÇÄ ‚Ñï) ‚Üí TwoSidedIdeal (MvPowerSeries œÉ Œ±) := fun d ‚Ü¶ by
  apply TwoSidedIdeal.mk' {f | ‚àÄ e ‚â§ d, coeff Œ± e f = 0}
  ¬∑ simp [coeff_zero]
  ¬∑ exact fun hf hg e he ‚Ü¶ by rw [map_add, hf e he, hg e he, add_zero]
  ¬∑ exact fun {f} hf e he ‚Ü¶ by simp only [map_neg, neg_eq_zero, hf e he]
  ¬∑ exact fun {f g} hg e he ‚Ü¶ by
      classical
      rw [coeff_mul]
      apply Finset.sum_eq_zero
      rintro uv huv
      convert MulZeroClass.mul_zero (coeff Œ± uv.fst f)
      exact hg  _ (le_trans (le_iff_exists_add'.mpr
        ‚ü®uv.fst, (Finset.mem_antidiagonal.mp huv).symm‚ü©) he)
  ¬∑ exact fun {f g} hf e he ‚Ü¶ by
      classical
      rw [coeff_mul]
      apply Finset.sum_eq_zero
      rintro uv huv
      convert MulZeroClass.zero_mul (coeff Œ± uv.snd g)
      exact hf _ (le_trans (le_iff_exists_add.mpr ‚ü®uv.2, (Finset.mem_antidiagonal.mp huv).symm‚ü©) he)

variable {œÉ : Type*} {Œ± : Type*} [Ring Œ±]

/-- A power series `f` belongs to the ideal `basis œÉ Œ± d` if and only if `coeff Œ± e f = 0` for all
`e ‚â§ d`. -/
theorem mem_basis_iff {f : MvPowerSeries œÉ Œ±} {d : œÉ ‚Üí‚ÇÄ ‚Ñï} :
    f ‚àà basis œÉ Œ± d ‚Üî ‚àÄ e ‚â§ d, coeff Œ± e f = 0 := by
  simp [basis]

/-- If `e ‚â§ d`, then we have the inclusion of ideals `basis œÉ Œ± d ‚â§ basis œÉ Œ± e`. -/
theorem basis_le {e d : œÉ ‚Üí‚ÇÄ ‚Ñï} (hed : e ‚â§ d) : basis œÉ Œ± d ‚â§ basis œÉ Œ± e :=
  fun _ => forall_imp (fun _ h ha => h (le_trans ha hed))

/-- `basis œÉ Œ± d ‚â§ basis œÉ Œ± e` if and only if `e ‚â§ d`. -/
theorem basis_le_iff [Nontrivial Œ±] {d e : œÉ ‚Üí‚ÇÄ ‚Ñï} :
    basis œÉ Œ± d ‚â§ basis œÉ Œ± e ‚Üî e ‚â§ d := by
  refine ‚ü®?_, basis_le‚ü©
  simp only [basis]
  intro h
  rw [‚Üê inf_eq_right]
  apply le_antisymm inf_le_right
  by_contra h'
  simp only [TwoSidedIdeal.le_iff, TwoSidedIdeal.coe_mk', setOf_subset_setOf] at h
  specialize h (monomial Œ± e 1) _
  ¬∑ intro e' he'
    apply coeff_monomial_ne
    intro hee'
    rw [hee'] at he'
    apply h'
    exact le_inf_iff.mpr ‚ü®he', le_rfl‚ü©
  ¬∑ apply one_ne_zero' Œ±
    convert h e le_rfl
    rw [coeff_monomial_same]

/-- The function `basis œÉ Œ±` is antitone. -/
theorem basis_antitone : Antitone (basis œÉ Œ±) := fun _ _ h => basis_le h

/-- The function `basis œÉ Œ±` is strictly antitone. -/
theorem basis_strict_anti [Nontrivial Œ±] : StrictAnti (basis œÉ Œ±) :=
  strictAnti_of_le_iff_le (fun _ _ ‚Ü¶ basis_le_iff.symm)

variable (œÉ Œ±) in
theorem ringSubgroupsBasis :
    RingSubgroupsBasis (fun d ‚Ü¶ (basis œÉ Œ± d).asIdeal.toAddSubgroup) where
  inter d e := ‚ü®d ‚äî e, basis_antitone.map_sup_le d e‚ü©
  mul d := ‚ü®d, fun f ‚Ü¶ by
    simp only [Submodule.coe_toAddSubgroup, mem_mul]
    rintro ‚ü®x, hx, y, hy, rfl‚ü©
    exact Ideal.mul_mem_left _ _ hy‚ü©
  leftMul f d := ‚ü®d, fun g hg ‚Ü¶ (basis œÉ Œ± d).mul_mem_left f g hg‚ü©
  rightMul f d := ‚ü®d, fun g hg ‚Ü¶ by
    intro e he
    simp only [Submodule.coe_toAddSubgroup, TwoSidedIdeal.coe_asIdeal,
      mem_coe, sub_zero, mem_basis_iff] at hg ‚ä¢
    classical
    rw [coeff_mul]
    apply Finset.sum_eq_zero
    rintro ‚ü®i, j‚ü© h
    rw [hg i (le_trans ?_ he), zero_mul]
    simp only [‚Üê Finset.mem_antidiagonal.mp h, le_self_add]‚ü©

section DiscreteTopology

-- We endow MvPowerSeries œÉ Œ± with the product topology.
open WithPiTopology

variable [TopologicalSpace Œ±] [DiscreteTopology Œ±]

/-- If the coefficient ring `Œ±` is endowed with the discrete topology, then for every `d : œÉ ‚Üí‚ÇÄ ‚Ñï`,
`‚Üë(basis œÉ Œ± d) ‚àà ùìù (0 : MvPowerSeries œÉ Œ±)`. -/
theorem basis_mem_nhds_zero (d : œÉ ‚Üí‚ÇÄ ‚Ñï) :
    (basis œÉ Œ± d : Set (MvPowerSeries œÉ Œ±)) ‚àà ùìù 0 := by
  classical
  rw [nhds_pi, Filter.mem_pi]
  use Finset.Iic d, Finset.finite_toSet _, (fun e => if e ‚â§ d then {0} else univ)
  constructor
  ¬∑ intro e
    split_ifs
    ¬∑ simp only [nhds_discrete, Filter.mem_pure, mem_singleton_iff]
      rfl
    ¬∑ simp only [Filter.univ_mem]
  ¬∑ intro f
    simp only [Finset.coe_Iic, mem_pi, mem_Iic, mem_ite_univ_right, mem_singleton_iff, mem_coe]
    rw [mem_basis_iff]
    exact forall_imp (fun e h he => h he he)

lemma mem_nhds_zero_iff {U : Set (MvPowerSeries œÉ Œ±)} :
    U ‚àà ùìù 0 ‚Üî ‚àÉ d, {b | b ‚àà basis œÉ Œ± d} ‚äÜ U := by
  refine ‚ü®?_ , fun ‚ü®d, hd‚ü© ‚Ü¶ Filter.sets_of_superset _ (basis_mem_nhds_zero d) hd‚ü©
  ¬∑ rw [nhds_pi, Filter.mem_pi]
    rintro ‚ü®D, hD, t, ht, ht'‚ü©
    use Finset.sup hD.toFinset id
    apply subset_trans _ ht'
    intros f hf e he
    simp only [‚Üê coeff_apply Œ± f e,
      sub_zero f ‚ñ∏ hf e (id_eq e ‚ñ∏ Finset.le_sup (hD.mem_toFinset.mpr he))]
    exact mem_of_mem_nhds (ht e)

/-- If the coefficient ring `Œ±` is endowed with the discrete topology, then the pointwise
topology on `MvPowerSeries œÉ Œ±)` agrees with the topology generated by `MvPowerSeries.basis`. -/
theorem topology_eq_ideals_basis_topology :
    MvPowerSeries.WithPiTopology.instTopologicalSpace Œ± =
      (ringSubgroupsBasis œÉ Œ±).toRingFilterBasis.topology := by
  rw [TopologicalAddGroup.ext_iff inferInstance inferInstance]
  ext s
  simp [mem_nhds_zero_iff, ((ringSubgroupsBasis œÉ Œ±).hasBasis_nhds 0).mem_iff]

/-- The topology on `MvPowerSeries` is a linear topology when the ring of coefficients has
the discrete topology. -/
instance : IsLinearTopology (MvPowerSeries œÉ Œ±) :=
  IsLinearTopology.mk_of_twoSidedIdeal (p := fun _ ‚Ü¶ True) (s := fun d ‚Ü¶ basis œÉ Œ± d)
    (Filter.HasBasis.mk fun s ‚Ü¶ by simp [mem_nhds_zero_iff])

end DiscreteTopology

end LinearTopology

end MvPowerSeries
