/-
Copyright (c) 2024 Jou Glasheen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jou Glasheen, Amelia Livingston, Jujian Zhang, Kevin Buzzard
-/
import Mathlib.RingTheory.Ideal.QuotientOperations
import Mathlib.RingTheory.Ideal.Pointwise
import Mathlib.Algebra.Polynomial.GroupRingAction
import Mathlib.FieldTheory.Finite.Basic

/-!

# Frobenius elements

Frobenius elements in Galois groups.

## Statement of the theorem

Say `L/K` is a finite Galois extension of number fields, with integer rings `B/A`,
and say `Q` is a maximal ideal of `B` dividing `P` of `A`. This file contains the
construction of an element `Frob Q` in `Gal(L/K)`, and a proof that
modulo `Q` it raises elements to the power `q := |A/P|`.

In fact we work in more generality, with a construction which also applies in the function
field setting.

We start with `A ‚äÜ B` an extension of integral domains, let `G` be `B ‚âÉ‚Çê[A] B`,
and assume that `A` equals the `G`-invariants of `B` (this assumption is true in the applications
to number fields and function fields). Given `Q/P` a compatible pair of maximal ideals, under the
assumption that `A/P` is a finite field of size `q` and `B/Q` is also finite,
we construct an element `Frob_Q` in `G` and prove:

1) `Frob_Q ‚Ä¢ Q = Q`;
2) `x ^ q ‚â° Frob_Q x (mod Q)`.

Note that if `Q` is ramified, there is more than one choice of `Frob_Q` satisfying the defining
equations; for example if `B=‚Ñ§[i]` and `A=‚Ñ§` then both the identity and complex conjugation
work for `Frob Q` if `Q=(1+i)`. In this situation `Frob` returns a random one (i.e. it's opaque;
all we know is that it satisfies the two properties).

In the applications, `galRestrict` will provide us with the isomorphism `L ‚âÉ‚Çê[K] L ‚âÉ* B ‚âÉ‚Çê[A] B`
which we need to construct the actual Frobenius elements in Galois groups.

## The construction

We follow a proof in footnote 2 on p141 of [J. S. Milne,  *Algebraic Number Theory*][milne-ANT]

The Galois orbit of `Q` consists of `Q` and possibly some other primes `Q'`. The unit group
`(B/Q)À£` is finite and hence cyclic; so by the Chinese Remainder Theorem we may choose `y ‚àà B`
which reduces to a generator mod `Q` and to 0 modulo all other `Q'` in the Galois orbit of `Q`.

The polynomial `F = ‚àè (X - œÉ y)`, the product running over `œÉ` in the Galois group, is in `B[X]`
and is Galois-stable so is in fact in `A[X]`. Hence if `Fbar` is `F mod Q`
then `Fbar` has coefficients in `A/P=ùîΩ_q` and thus `Fbar(y^q)=Fbar(y)^q=0`, meaning that `y^q`
is a root of `F` mod `Q` and thus congruent mod `Q` to `œÉ y` for some `œÉ`. We define `Frob Q` to
be this `œÉ` (more precisely, any such `œÉ`).

## The proof

We know `œÉ y ‚â° y ^ q ‚â† 0 mod Q`. Hence `œÉ y ‚àâ Q`, and thus `y ‚àâ œÉ‚Åª¬π Q`. But `y ‚àà Q'` for all
nontrivial conjugates `Q'` of `Q`, hence `œÉ‚Åª¬π Q = Q` and thus `Q` is `œÉ`-stable.

Hence `œÉ` induces a field automorphism of `B/Q` and we know it's `x ‚Ü¶ x^q` on a generator,
so it's `x ‚Ü¶ x^q` on everything.

## Note

This work started its life as Jou Glasheen's final project for Kevin Buzzard's
Formalising Mathematics 2024 course.

## TODO

Check that the hypotheses are satisfied in the two main applications:

1) Finite Galois extensions of number fields and their integers or `S`-integers;
2) Finite Galois extensions of function fields over finite fields, and their `S`-integers for
   `S` a nonempty set of places;

Note that the theorem could also be used to construct the Frobenius element in the
case where `A` and `B` are finite fields, and `Q/P=0/0` (recovering the
classical theory of Frobenius elements).

-/

variable (A : Type*) [CommRing A] {B : Type*} [CommRing B] [Algebra A B]

open scoped Pointwise -- to get Galois action on ideals

namespace ArithmeticFrobenius -- So it will be absolutely clear which Frob we're talking about
-- some authors like to use "Geometric Frobenius", which is the the inverse of this one

namespace Abstract -- stuff in here is auxiliary variables etc, general stuff which will
-- work in number field and function field settings. This namespace is full of junk
-- like `y_spec` and `F_spec` which are auxiliary constructions needed for `Frob` and
-- which we will never need again.

/-

## Auxiliary variables

The noncomputable variables `g : (B ‚ß∏ Q)À£` (a generator),
`y : B` (congruent to `g` mod `Q` and to `0` mod all Galois conjugates of `Q`,
`F : B[X]` (the product of `X - œÉ y` as `œÉ` runs through the Galois group), and
`m : A[X]`, the descent of `F` to `A[X]` (it's Galois-stable).
-/

variable (Q : Ideal B) [Q.IsMaximal]

variable [Fintype (B ‚ß∏ Q)]

/-- An auxiliary definition needed in the definition of Frobenius elements;
`g` is a generator of the units of the finite field `B ‚ß∏ Q`.-/
noncomputable abbrev g : (B ‚ß∏ Q)À£ := (IsCyclic.exists_monoid_generator (Œ± := (B ‚ß∏ Q)À£)).choose

lemma g_spec : ‚àÄ (z : (B ‚ß∏ Q)À£), z ‚àà Submonoid.powers (g Q) :=
  (IsCyclic.exists_monoid_generator (Œ± := (B ‚ß∏ Q)À£)).choose_spec

variable [Fintype (B ‚âÉ‚Çê[A] B)] [DecidableEq (Ideal B)]

/-- An element `y` of `B` exists, which is congruent to `b` mod `Q`
and to `0` mod all other Galois conjugates of `Q` (if any).-/
lemma exists_y :
    ‚àÉ y : B, (y : B ‚ß∏ Q) = g Q ‚àß ‚àÄ Q' :
    Ideal B, Q' ‚àà MulAction.orbit (B ‚âÉ‚Çê[A] B) Q ‚Üí Q' ‚â† Q ‚Üí y ‚àà Q' := by
  let O : Set (Ideal B) := MulAction.orbit (B ‚âÉ‚Çê[A] B) Q
  have hO' : Finite (O : Type _) := Set.finite_range _
  have hmax (I : O) : Ideal.IsMaximal (I : Ideal B) := by
    rcases I with ‚ü®_, œÉ, rfl‚ü©
    convert Ideal.comap_isMaximal_of_surjective (K := Q) _ (AlgEquiv.surjective œÉ.symm)
    apply Ideal.pointwise_smul_eq_comap
  have hPairwise : Pairwise fun (I : O) (J : O) ‚Ü¶ IsCoprime (I : Ideal B) J := fun x y h ‚Ü¶ ‚ü®1, 1, by
    simp only [Ideal.one_eq_top, Ideal.top_mul]
    exact Ideal.IsMaximal.coprime_of_ne (hmax x) (hmax y) <| mt Subtype.ext h‚ü©
  obtain ‚ü®y, hy‚ü© := Ideal.exists_forall_sub_mem_ideal (Œπ := O) hPairwise
    (fun J ‚Ü¶ if J = ‚ü®Q, 1, by simp‚ü© then (Ideal.Quotient.mk_surjective (g Q : B ‚ß∏ Q)).choose else 0)
  refine ‚ü®y, ?_, ?_‚ü©
  ¬∑ specialize hy ‚ü®Q, 1, by simp‚ü©
    simp at hy
    rw [‚Üê (Ideal.Quotient.mk_surjective (g Q : B ‚ß∏ Q)).choose_spec]
    exact
      (Ideal.Quotient.mk_eq_mk_iff_sub_mem y
        (Ideal.Quotient.mk_surjective (I := Q) (g Q)).choose).mpr hy
  ¬∑ rintro Q' ‚ü®œÉ, rfl‚ü© hQ'
    specialize hy ‚ü®œÉ ‚Ä¢ Q, œÉ, rfl‚ü©
    simp_all

/-- An auxiliary definition needed for the definition of an abstract Frobenius
element; `y ‚àà B` reduces mod `Q` to a generator of the unit group,
and reduces mod `Q'` to `0` for any other maximal ideal Galois-conjugate to `Q`.
Its existence is trivial from the Chinese Remainder Theorem but we need an
explicit name for it. -/
noncomputable abbrev y : B :=
  (exists_y A Q).choose

lemma y_spec : ((y A Q : B) : B ‚ß∏ Q) = g Q ‚àß
    ‚àÄ Q' : Ideal B, Q' ‚àà MulAction.orbit (B ‚âÉ‚Çê[A] B) Q ‚Üí Q' ‚â† Q ‚Üí (y A Q) ‚àà Q' :=
  (exists_y A Q).choose_spec

lemma y_mod_Q : Ideal.Quotient.mk Q (y A Q) = g Q := (y_spec A Q).1

open scoped algebraMap

lemma y_mod_Q' : (((y A Q) : B) : B ‚ß∏ Q) = (g Q : B ‚ß∏ Q) := y_mod_Q A Q

lemma y_not_in_Q : (y A Q) ‚àâ Q :=
  mt Ideal.Quotient.eq_zero_iff_mem.mpr <| y_mod_Q A Q ‚ñ∏ (g Q).ne_zero

open Polynomial BigOperators

/-- `F : B[X]` is defined to be a product of linear factors `(X - œÑ ‚Ä¢ y)`; where
`œÑ` runs over `B ‚âÉ‚Çê[A] B`, and `y : B` is an element which generates `(B ‚ß∏ Q)À£`
and lies in `œÑ ‚Ä¢ Q` for all `œÑ ‚Ä¢ Q ‚â† Q`.-/
noncomputable abbrev F : B[X] := ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C (œÑ ‚Ä¢ (y A Q)))

lemma F_spec : F A Q = ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C (œÑ ‚Ä¢ (y A Q))) := rfl

variable {A Q} in
open Finset in
lemma F.smul_eq_self (œÉ :  B ‚âÉ‚Çê[A] B)  : œÉ ‚Ä¢ (F A Q) = F A Q := calc
  œÉ ‚Ä¢ F A Q = œÉ ‚Ä¢ ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C (œÑ ‚Ä¢ (y A Q))) := by rw [F_spec]
  _         = ‚àè œÑ : B ‚âÉ‚Çê[A] B, œÉ ‚Ä¢ (X - C (œÑ ‚Ä¢ (y A Q))) := smul_prod
  _         = ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C ((œÉ * œÑ) ‚Ä¢ (y A Q))) := by simp [smul_sub]
  _         = ‚àè œÑ' : B ‚âÉ‚Çê[A] B, (X - C (œÑ' ‚Ä¢ (y A Q))) := Fintype.prod_bijective (fun œÑ ‚Ü¶ œÉ * œÑ)
                                                      (Group.mulLeft_bijective œÉ) _ _ (fun _ ‚Ü¶ rfl)
  _         = F A Q := by rw [F_spec]

lemma F.y_eq_zero : (F A Q).eval (y A Q) = 0 := by
  simp [F_spec, eval_prod, Finset.prod_eq_zero (Finset.mem_univ (1 : B ‚âÉ‚Çê[A] B))]

open scoped algebraMap

-- This trick enables us to use `norm_cast` to make some proofs less painful.
/-- An auxiliary local instance, enabling us to coerce polynomials
cheaply from `A[X]` to `B[X]` given that `B` is an `A`-algebra.  -/
noncomputable local instance : Algebra A[X] B[X] :=
  RingHom.toAlgebra (Polynomial.mapRingHom (Algebra.toRingHom))

@[simp, norm_cast]
lemma _root_.Polynomial.coe_monomial' (n : ‚Ñï) (a : A) :
    ((monomial n a : A[X]) : B[X]) = monomial n (a : B) :=
  map_monomial _

lemma F.descent (h : ‚àÄ b : B, (‚àÄ œÉ : B ‚âÉ‚Çê[A] B, œÉ ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, b = a) :
    ‚àÉ m : A[X], (m : B[X]) = F A Q := by
  choose f hf using h
  classical
  let f' : B ‚Üí A := fun b ‚Ü¶ if h : ‚àÄ œÉ : B ‚âÉ‚Çê[A] B, œÉ b = b then f b h else 37
  use (‚àë x ‚àà (F A Q).support, (monomial x) (f' ((F A Q).coeff x)))
  ext N
  push_cast
  simp_rw [finset_sum_coeff, ‚Üê lcoeff_apply, lcoeff_apply, coeff_monomial,
    Finset.sum_ite_eq', mem_support_iff, ne_eq, ite_not, f']
  split
  ¬∑ next h => exact h.symm
  ¬∑ next h1 =>
    rw [dif_pos <| fun œÉ ‚Ü¶ ?_]
    ¬∑ refine (hf ?_ ?_).symm
    ¬∑ nth_rw 2 [‚Üê F.smul_eq_self œÉ]
      rfl

-- This will be true in applications to number fields etc. A Galois-invariant element
-- of `B` is in `A`.
variable (isGalois : ‚àÄ b : B, (‚àÄ œÉ : B ‚âÉ‚Çê[A] B, œÉ ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, b = a)

/-- An auxiliary polynomial, needed in the definition of Frobenius elements.
It's the descent to `A[X]` of a certain auxiliary Galois-invariant polynomial in `B[X]`. -/
noncomputable abbrev m := (F.descent A Q isGalois).choose

lemma m_spec : ((m A Q isGalois) : B[X]) = F A Q := (F.descent A Q isGalois).choose_spec

lemma m_spec' : (m A Q isGalois).map (algebraMap A B) = F A Q := by
  rw [‚Üê m_spec A Q isGalois]
  rfl

-- Amelia's trick to insert "let P be the ideal under Q" into the typeclass system
variable (P : Ideal A) [P.IsMaximal] [Algebra (A ‚ß∏ P) (B ‚ß∏ Q)] [IsScalarTower A (A‚ß∏P) (B‚ß∏Q)]
variable {A} -- now can be implicit because `P`

lemma m.y_mod_P_eq_zero : Polynomial.aeval (‚Üë(y A Q) : B ‚ß∏ Q) (m A Q isGalois) = 0 := by
  rw [‚Üê aeval_map_algebraMap B, m_spec', algebraMap.coe_def, aeval_algebraMap_apply,
    coe_aeval_eq_eval, F.y_eq_zero A Q, map_zero]

open scoped Polynomial

variable [Fintype (A‚ß∏P)]

lemma m.mod_P_y_pow_q_eq_zero' :
    aeval ((algebraMap B (B‚ß∏Q) (y A Q)) ^ (Fintype.card (A‚ß∏P)))  (m A Q isGalois) = 0 := by
  let foobar : Field (A‚ß∏P) := ((Ideal.Quotient.maximal_ideal_iff_isField_quotient P).mp ‚Äπ_‚Ä∫).toField
  rw [‚Üê aeval_map_algebraMap (A‚ß∏P), FiniteField.aeval_pow_card, ‚Üê algebraMap.coe_def,
    aeval_map_algebraMap, m.y_mod_P_eq_zero, zero_pow Fintype.card_ne_zero]

lemma F.mod_Q_y_pow_q_eq_zero' :
    aeval ((algebraMap B (B‚ß∏Q) (y A Q)) ^ (Fintype.card (A‚ß∏P))) (F A Q) = 0 := by
  rw [‚Üê m_spec' A Q isGalois, aeval_map_algebraMap, m.mod_P_y_pow_q_eq_zero']

lemma _root_.Polynomial.aeval_finset_prod.{u, v, y} {R : Type u} {S : Type v} {Œπ : Type y}
    [CommSemiring R] [CommSemiring S] [Algebra R S] (s : Finset Œπ) (g : Œπ ‚Üí R[X]) (x : S) :
  aeval x (‚àè i ‚àà s, g i) = (‚àè i ‚àà s, aeval x (g i)) := eval‚ÇÇ_finset_prod (algebraMap R S) s g x

lemma exists_Frob : ‚àÉ œÉ : B ‚âÉ‚Çê[A] B, œÉ (y A Q) - (y A Q) ^ (Fintype.card (A‚ß∏P)) ‚àà Q := by
  have := F.mod_Q_y_pow_q_eq_zero' Q isGalois P
  rw [F_spec, aeval_finset_prod, Finset.prod_eq_zero_iff] at this
  obtain ‚ü®œÉ, -, hœÉ‚ü© := this
  use œÉ
  simp only [Ideal.Quotient.algebraMap_eq, AlgEquiv.smul_def, map_sub, aeval_X, aeval_C,
    sub_eq_zero] at hœÉ
  exact (Submodule.Quotient.eq Q).mp (hœÉ.symm)

/-- An auxiliary arithmetic Frobenius element, in the automorphism group of the integer ring
rather than the global field itself. -/
noncomputable abbrev Frob := (exists_Frob Q isGalois P).choose

lemma Frob_spec : (Frob Q isGalois P) ‚Ä¢ (y A Q) - (y A Q) ^ (Fintype.card (A‚ß∏P)) ‚àà Q :=
  (exists_Frob Q isGalois P).choose_spec

lemma Frob_Q : Frob Q isGalois P ‚Ä¢ Q = Q := by
  rw [smul_eq_iff_eq_inv_smul]
  by_contra h
  have hy : y A Q ‚àà (Frob Q isGalois P)‚Åª¬π ‚Ä¢ Q := (y_spec A Q).2 _ ‚ü®_, rfl‚ü© (Ne.symm h)
  have hy2 : (Frob Q isGalois P) ‚Ä¢ (y A Q) ‚àà Q := by
    rwa [Ideal.pointwise_smul_eq_comap] at hy
  have this := Q.sub_mem hy2 <| Frob_spec Q isGalois P
  simp only [sub_sub_cancel] at this
  apply y_not_in_Q A Q <| Ideal.IsPrime.mem_of_pow_mem (show Q.IsPrime by infer_instance) _ this

lemma Frob_Q_eq_pow_card (x : B) : Frob Q isGalois P x - x ^ (Fintype.card (A‚ß∏P)) ‚àà Q := by
  by_cases hx : x ‚àà Q
  ¬∑ refine Q.sub_mem ?_ (Q.pow_mem_of_mem hx _ Fintype.card_pos)
    nth_rw 2 [‚Üê Frob_Q Q isGalois P]
    change (Frob Q isGalois P) ‚Ä¢ x ‚àà _
    rw [Ideal.pointwise_smul_eq_comap, Ideal.mem_comap]
    convert hx
    exact inv_smul_smul (Frob Q isGalois P) _
  ¬∑ letI : Field (B ‚ß∏ Q) := ((Ideal.Quotient.maximal_ideal_iff_isField_quotient Q).mp ‚Äπ_‚Ä∫).toField
    let xbar : (B ‚ß∏ Q)À£ := Units.mk0 (x : B ‚ß∏ Q) <|
      mt (fun h ‚Ü¶ (Submodule.Quotient.mk_eq_zero Q).mp h) hx
    obtain ‚ü®n, (hn : g Q ^ n = xbar)‚ü© := g_spec Q xbar
    have hn2 : (g Q : B ‚ß∏ Q) ^ n = xbar := by exact_mod_cast hn
    change _ = (x : B ‚ß∏ Q) at hn2
    rw [‚Üê Ideal.Quotient.cast_eq_cast_iff_sub_mem]
    push_cast
    rw [‚Üê hn2]
    have := Frob_spec Q isGalois P
    rw [‚Üê Ideal.Quotient.cast_eq_cast_iff_sub_mem] at this
    push_cast at this
    rw [‚Üê y_mod_Q' A Q, pow_right_comm, ‚Üê this]
    norm_cast
    rw [Ideal.Quotient.cast_eq_cast_iff_sub_mem, ‚Üê smul_pow']
    change (Frob Q isGalois P) ‚Ä¢ x - _ ‚àà _
    rw [‚Üê smul_sub]
    nth_rw 3 [ ‚Üê Frob_Q Q isGalois P]
    rw [Ideal.smul_mem_pointwise_smul_iff, ‚Üê Ideal.Quotient.cast_eq_cast_iff_sub_mem,
      ‚Üê hn2, ‚Üê y_mod_Q A Q]
    norm_cast
